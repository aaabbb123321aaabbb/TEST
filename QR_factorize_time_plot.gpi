set terminal svg size 900,600 enhanced font 'Arial,16' background rgb "white"
set output 'QR_factorize_time_plot.svg'

set title "QR-factorize an N×N matrix"
set xlabel "N"
set ylabel "Time [seconds]"
set grid

set fit logfile 'QR_factorize_time_fit.log'

# a = ... er start get for a 
a = 2.3e-9
f(x) = a * x**3
fit f(x) 'QR_factorize_time.txt' using 1:2 via a
set label sprintf("Fit: a = %.2e", a) at graph 0.05, 0.90 font "Arial,16" front

plot 'QR_factorize_time.txt' using 1:2 with linespoints lt rgb "blue" lw 2 pt 7 title "Measured time", \
     f(x) with lines lt rgb "red" lw 2 title "Fit: a·N³"
